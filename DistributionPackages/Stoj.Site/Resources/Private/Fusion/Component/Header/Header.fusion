prototype(Stoj.Site:Component.Header) < prototype(Neos.Fusion:Component) {
    homeLink = Neos.Neos:NodeUri {
        node = ${site}
    }
    menuItems = Neos.Neos:MenuItems {
        startingPoint = ${site}
        maximumLevels = 2
    }
    logo = Neos.Fusion:ResourceUri {
    path = 'resource://Stoj.Site/Public/Frontend/svg/logo.svg'
    }

    ##
    # If you want to use a default rendering for a menu, you can just use:
    ##
    # <Neos.Neos:Menu
    #     startingPoint={site}
    #     attributes.class="navigation"
    #     normal.attributes.class="navigation-item"
    #     active.attributes.class="navigation-item navigation-item--state-active"
    #     current.attributes.class="navigation-item navigation-item--state-current"
    #     />
    ##

    renderer = afx`
        <header class="main-header" id="menu">
            <a class="logo" href={props.homeLink}>
                <img
                class="logo-svg"
                src={props.logo}
                alt=""/>
            </a>
            <div class="menu">
                <div class="menu-items">
                    <Stoj.Site:Component.Header.MenuItemsRenderer items={props.menuItems} />
                </div>
                <div class="menu-btn">
                    <a href="#">
                        <span></span>
                        <span></span>
                    </a>
                </div>
            </div>
            <div class="header-gradient"></div>
        </header>
        <div class="contact-btn">
            <a href="mailto:halloj@hallojstoj.dk">
                <button type="button" name="button">Sig hall√∏j</button>
            </a>
        </div>
    `

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
